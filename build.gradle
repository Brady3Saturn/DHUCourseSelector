buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}


plugins {
    id 'com.jfrog.bintray' version '1.7.3' apply false
    id 'com.github.ben-manes.versions' version '0.14.0' apply false
    id 'me.tatarka.retrolambda' version '3.6.0' apply false
    id 'com.github.hierynomus.license' version '0.13.1' apply false
    id 'net.nemerosa.versioning' version '2.6.0' apply false
    id "org.sonarqube" version "2.6.1"
}

task wrapper(type: Wrapper) {
   gradleVersion = '4.6' //version required
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    /**
	 * 1st approach: Setting encoding during compilation in Java and Test classes
	 */
	compileJava.options.encoding = "UTF-8"
	compileTestJava.options.encoding = "UTF-8"
}

subprojects {
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'net.nemerosa.versioning'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://sevntu-checkstyle.github.com/sevntu.checkstyle/maven2' }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    apply from: rootProject.file('gradle/code-coverage.gradle')
    apply from: rootProject.file('gradle/checkstyle.gradle')
    apply from: rootProject.file('gradle/findbugs.gradle')
    apply from: rootProject.file('gradle/javafx.gradle')

    if(JavaVersion.current() == JavaVersion.VERSION_1_9 || JavaVersion.current() ==
            JavaVersion.VERSION_1_10) {
        sourceCompatibility = 1.9
        targetCompatibility = 1.9
    } else if(JavaVersion.current() == JavaVersion.VERSION_1_8) {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

project(':libDHUCourseSelecter') {
    //just for test
    apply plugin: 'application'

    mainClassName = 'tk.dcmmcc.DHUCourseSelecterCLI'

    dependencies {
        //本地libs
        compile files('libs/commons-codec-1.9.jar',
                'libs/commons-httpclient-3.1.jar',
                'libs/commons-logging-1.2.jar',
                'libs/jsoup-1.10.3.jar')
    }
}

project(':DHUCourseSelecterClient') {
    apply plugin: 'application'

    mainClassName = 'tk.dcmmcc.Main'

    // support for java9/java10
    compileJava {
        if(JavaVersion.current() == JavaVersion.VERSION_1_9 || JavaVersion.current() ==
                JavaVersion.VERSION_1_10) {
            options.compilerArgs += ["--add-modules", "java.xml.bind"]
            options.compilerArgs += ["--add-exports", "java.xml.bind/com.sun.istack.internal=ALL-UNNAMED"]
            options.compilerArgs += ["--add-modules", "java.xml.ws.annotation"]
        }
    }

    compileTestJava {
        if(JavaVersion.current() == JavaVersion.VERSION_1_9 || JavaVersion.current() == 
            JavaVersion.VERSION_1_10) {
            options.compilerArgs += ["--add-modules", "java.xml.bind"]
            options.compilerArgs += ["--add-exports", "java.xml.bind/com.sun.istack.internal=ALL-UNNAMED"]
            options.compilerArgs += ["--add-modules", "java.xml.ws.annotation"]
        }
    }
    
    dependencies {
        compile project(':libDHUCourseSelecter')

        //本地libs
        // compile files('libs/tilesfx-1.4.4.jar')
	
		compile 'eu.hansolo:tilesfx:1.5.5'

        if(JavaVersion.current() == JavaVersion.VERSION_1_9 || JavaVersion.current() == 
			JavaVersion.VERSION_1_10) {
        	compile 'com.jfoenix:jfoenix:9.0.3' // Java 9
        	compile 'org.controlsfx:controlsfx:9.0.0'
            // FontAwesome (Bintray)
            compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-5'
            compile 'de.jensd:fontawesomefx-commons:9.1.2'
        } else if(JavaVersion.current() == JavaVersion.VERSION_1_8) {
        	compile 'org.controlsfx:controlsfx:8.40.13'
        	compile 'com.jfoenix:jfoenix:8.0.3' // Java 8
            // FontAwesome (Bintray)
            compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-5'
            compile 'de.jensd:fontawesomefx-commons:8.11'
        }

        // NOTE: the latest version _is_ 8.0.1!
        // 8.0.7 was published by mistake
        compile 'io.datafx:datafx:8.0.1'
        compile 'io.datafx:flow:8.0.1'

        // annotation
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
        compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.1'
    }
}
